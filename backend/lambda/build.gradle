apply from: "${rootProject.rootDir}/gradle/java.gradle"

sourceSets {
    processorExtensions {
        java.srcDir file('src/processorExtensions/java')
        resources.srcDir file('src/processorExtensions/resources')
        println configurations
        compileClasspath += configurations.processor
        runtimeClasspath += output
    }
}

configurations {

}

dependencies {
    compile (
            project(':backend:event-store'),

            'com.amazonaws:aws-lambda-java-core:1.1.0',
            'io.symphonia:lambda-logging:1.0.1',
            'com.fasterxml.jackson.core:jackson-core:2.8.5',
            'com.fasterxml.jackson.core:jackson-databind:2.8.5',
            'com.fasterxml.jackson.core:jackson-annotations:2.8.5',

            'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.5',
            'com.fasterxml.jackson.module:jackson-module-parameter-names:2.8.5',
            'com.amazonaws.serverless:aws-serverless-java-container-jersey:1.1.3',
            'org.glassfish.jersey.inject:jersey-hk2:2.26',
            'com.auth0:java-jwt:3.4.0',
            'com.auth0:jwks-rsa:0.6.0',

    )

    processor (
            'org.immutables:value:2.5.3',
            'com.google.auto.service:auto-service:1.0-rc4',
            'org.derive4j:derive4j:0.12.4',
            sourceSets.processorExtensions.output,
    )

    testCompile (
            'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.26',
            'com.palantir.docker.compose:docker-compose-rule-junit4:0.32.1',
            'org.slf4j:slf4j-simple:1.7.25',
            'junit:junit:4.12',
            'org.assertj:assertj-core:3.8.0',
    )
}

// http://docs.aws.amazon.com/lambda/latest/dg/create-deployment-pkg-zip-java.html
task buildZip(type: Zip) {
    baseName = "${rootProject.name}-lambda"
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

build.dependsOn buildZip

task runLocalServer(type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath

    main = 'mealfu.LocalServer'

    // arguments to pass to the application
    args '9876'
}